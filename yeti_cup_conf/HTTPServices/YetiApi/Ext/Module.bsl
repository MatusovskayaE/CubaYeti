Функция YetiApiGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	// получим имя метода
	ИмяМетода = Запрос.ПараметрыURL.Получить("ИмяМетода");
	
	// обработка зависит от имени метода
	Если ИмяМетода = "getpage" Тогда
		
		// получение параметров
		Сайт		= Запрос.ПараметрыЗапроса.Получить("site");
		Страница	= Запрос.ПараметрыЗапроса.Получить("page");
		
		Результат	= РаботаСHttpСервисом.ПолучитьСтраницу(Сайт, Страница);
		
	ИначеЕсли ИмяМетода = "getchampsbyyear" Тогда

		// получение параметров
		Год			= Запрос.ПараметрыЗапроса.Получить("year");
		
		Результат	= РаботаСHttpСервисом.ПолучитьЧемпионатыЗаПериод(Дата(Число(Год),1,1), Дата(Число(Год),12,31,23,59,59));
		
	ИначеЕсли ИмяМетода = "getresultbyyear" Тогда

		// получение параметров
		Год			= Запрос.ПараметрыЗапроса.Получить("year");
		
		Результат	= РаботаСHttpСервисом.ПолучитьДостиженияЗаПериод(Дата(Число(Год),1,1), Дата(Число(Год),12,31,23,59,59));
		
	Иначе
		
		Ответ.КодСостояния = 405;
		Результат = "Отсутствует метод " + ИмяМетода;
		
	КонецЕсли;
	
	Ответ.УстановитьТелоИзСтроки(Результат,КодировкаТекста.UTF8);
	Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
	
	Возврат Ответ;
	
КонецФункции
