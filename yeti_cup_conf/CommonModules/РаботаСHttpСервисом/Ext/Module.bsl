///////////////////////////////////////////////////////////////////////////////////////////////////
// Получение страницы из справочника
///////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьСтраницу(Сайт, Страница) Экспорт
	
	Результат		= "";
	
	// получим "сайт"
	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Сайты.Ссылка КАК Ссылка
	            	  |ПОМЕСТИТЬ Сайты
	            	  |ИЗ
	            	  |	Справочник.Сайты КАК Сайты
	            	  |ГДЕ
	            	  |	СОКРЛП(Сайты.Наименование) = &Сайт
	            	  |			И НЕ Сайты.ПометкаУдаления
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ
	            	  |	Сайты.Ссылка КАК Ссылка
	            	  |ИЗ
	            	  |	Справочник.Сайты КАК Сайты
	            	  |ГДЕ
	            	  |	Сайты.Ссылка В ИЕРАРХИИ
	            	  |			(ВЫБРАТЬ
	            	  |				Сайты.Ссылка КАК Ссылка
	            	  |			ИЗ
	            	  |				Сайты КАК Сайты)
	            	  |	И СОКРЛП(Сайты.Наименование) = &Страница
	            	  |			И НЕ Сайты.ПометкаУдаления";
	Запрос.УстановитьПараметр("Сайт",		Сайт);
	Запрос.УстановитьПараметр("Страница",	Страница);
	
	Выборка			= Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ВремФайл				= ПолучитьИмяВременногоФайла(Строка(Выборка.Ссылка.ТипЭлемента));
		Выборка.Ссылка.Данные.Получить().Записать(ВремФайл);
		
		ТекстовыйДокумент		= Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ВремФайл, КодировкаТекста.UTF8);
				
		Результат	= ТекстовыйДокумент.ПолучитьТекст();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Получение списка чемпионатов за период
///////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьЧемпионатыЗаПериод(ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Championahips.Наименование КАК Наименование,
	            	  |	Championahips.descriptions КАК Описание,
	            	  |	Championahips.location КАК МестоПроведения
	            	  |ИЗ
	            	  |	Справочник.Championahips КАК Championahips
	            	  |ГДЕ
	            	  |	Championahips.begin_date МЕЖДУ &ДатаНачала И &ДатаОкончания";
	Запрос.УстановитьПараметр("ДатаНачала",		ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",	ДатаОкончания);
	
	Данные			= Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Наименование",,"Наименование");
	Данные.Колонки.Добавить("Описание",,"Описание");
	Данные.Колонки.Добавить("МестоПроведения",,"Место проведения");
	
	Выборка			= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовСтр		= Данные.Добавить();
		
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		
	КонецЦикла;
	
	Возврат ФормированиеHTML.СформироватьТаблицу("", Данные);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////////
// Получение достижений за период
///////////////////////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьДостиженияЗаПериод(ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ
	            	  |	Dostizheniya.profile КАК profile,
	            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Dostizheniya.championate) КАК championate,
	            	  |	СУММА(Dostizheniya.result) КАК result
	            	  |ПОМЕСТИТЬ Результаты
	            	  |ИЗ
	            	  |	РегистрСведений.Dostizheniya КАК Dostizheniya
	            	  |ГДЕ
	            	  |	Dostizheniya.championship_date МЕЖДУ &ДатаНачала И &ДатаОкончания
	            	  |
	            	  |СГРУППИРОВАТЬ ПО
	            	  |	Dostizheniya.profile
	            	  |;
	            	  |
	            	  |////////////////////////////////////////////////////////////////////////////////
	            	  |ВЫБРАТЬ
	            	  |	Результаты.profile КАК Участник,
	            	  |	Результаты.championate КАК Чемпионатов,
	            	  |	Результаты.result КАК ОбщийРезультат,
	            	  |	ВЫРАЗИТЬ(Результаты.result / Результаты.championate КАК ЧИСЛО(15, 2)) КАК СреднийРезультат
	            	  |ИЗ
	            	  |	Результаты КАК Результаты
	            	  |
	            	  |УПОРЯДОЧИТЬ ПО
	            	  |	СреднийРезультат УБЫВ";
	Запрос.УстановитьПараметр("ДатаНачала",		ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",	ДатаОкончания);
	
	Данные			= Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Участник",,"Участник");
	Данные.Колонки.Добавить("Чемпионатов",,"Кол-во чемпионатов");
	Данные.Колонки.Добавить("ОбщийРезультат",,"Общий результат");
	Данные.Колонки.Добавить("СреднийРезультат",,"Рейтинг");
	
	Выборка			= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовСтр		= Данные.Добавить();
		
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка,,"Участник");
		
		НовСтр.Участник	= Выборка.Участник.last_name + " " + Выборка.Участник.fitst_name + " " + Выборка.Участник.second_name;
		
	КонецЦикла;
	
	Возврат ФормированиеHTML.СформироватьТаблицу("", Данные);
	
	
КонецФункции